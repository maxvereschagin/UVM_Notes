[[16. Using analysis port in testbench]]
Идея в следующем: есть класс монитора, который собирает данные с шины DUT-а, кладет их в удобную форму (формат) и отправляет их в классы "потребители", которые на основании полученных данных делают какой-то анализ: coverage, scoreboard и так далее. И чтобы отделить реализацию монитора от классов "потребителей", и упростить передачу данных между объектами, в uvm есть параметризуемый класс **uvm_analysis_port  #(type)** обладающий методом write(). 
```verilog
uvm_analysis_port #(command_s) ap;

function void build_phase(uvm_phase phase);
	ap = new("ap",this);
endfunction : build_phase

function void write_to_monitor(byte A, byte B, bit[2:0] op);
	command_s cmd;
	cmd.A = A;
	cmd.B = B;
	cmd.op = op2enum(op);
	$display("COMMAND MONITOR: A:0x%2h B:0x%2h op: %s", A, B, cmd.op.name());
	ap.write(cmd);
endfunction : write_to_monitor
```

Через этот метод (write()) каждый подписчик (наследник класса uvm_subscriber) получает доступ к передаваемым данным, так как при объявлении класса ему передается тип передаваемых данных:
```verilog
class coverage extends uvm_subscriber #(command_s);
`uvm_component_utils(coverage)
```

Каждый наследник это класса обладает методом write(), через который данные из uvm_analysis_port присваиваются во внутренние переменные класса:
```verilog
byte unsigned A;
byte unsigned B;
operation_t op_set;

function void write(command_s t);
	A = t.A;
	B = t.B;
	op_set = t.op;
endfunction : write
```

Таким образом реализация подписчика может быть полностью независима от DUT-а, главное чтоб через метод write и uvm_analysis_port передавались данные 




Когда недостаточно одного канала данных, например scoreboard-у нужно получать данные из разных мониторов или тип данных разный, то используется класс: 
```verilog
uvm_tlm_analysis_fifo #(type) a_tlm_fifo_h;
```

Этот класс инстанцируется в классе подписчика и аллоцируется конструктором класса в build_phase 
(это в принципе наверно всегда так, если в классе нужен другой класс, то аллоцировать его в build_phase). При этом в классе "источника данных" дополнительных действий не требуется, в нем достаточно просто объявить `uvm_analysis_port`, а в подписчике истанцировать все объекты класса `uvm_tlm_analysis_fifo` с нужным типом данных: 
```verilog
class scoreboard extends uvm_subscriber #(shortint);
`uvm_component_utils(scoreboard);

uvm_tlm_analysis_fifo #(command_s) cmd_f;

function void build_phase(uvm_phase phase);
	cmd_f = new ("cmd_f", this);
endfunction : build_phase

function void write(shortint t);
	shortint predicted_result;
	command_s cmd;
	cmd.op = no_op;
	do
		if (!cmd_f.try_get(cmd)) $fatal(1, "No command in self checker");
	while ((cmd.op == no_op) || (cmd.op == rst_op));
	case (cmd.op)
		add_op: predicted_result = cmd.A + cmd.B;
		and_op: predicted_result = cmd.A & cmd.B;
		xor_op: predicted_result = cmd.A ^ cmd.B;
		mul_op: predicted_result = cmd.A * cmd.B;
	endcase // case (op_set)
	if (predicted_result != t)
		$error (
		"FAILED: A: %2h B: %2h op: %s actual result: %4h expected: %4h",
			cmd.A, cmd.B, cmd.op.name(), t, predicted_result);
endfunction : write
// положить данные из cmd_f ссылки в переменную cmd
// сравнить получившейся результат эталонной модели с результатом из DUT-а (predicted_result != t)
```

Затем, в классе env (наследник `uvm_env`)  мы должны объявить placeholder-ы всех объектов (scoreboard, coverage, monitor, tester), создать объекты в build_phase, и соединить классы "потребителей данных" (coverage, scoreboard) с классами источников данных с DUT-a (monitor). 
Поскольку analysis порт это односторонние общение, то и подключать нужно меньше объектов (в классе env не нужно объявлять uvm_tlm_fifo для подключения объектов через фифо посредника, а можно напрямую): 
```verilog
function void connect_phase(uvm_phase phase);
	// соединить uvm_analyse_port result_monitor-а с analyse_port-ом класса          // скорборда для передачи через метод write()
	result_monitor_h.ap.connect(scoreboard_h.analysis_export);

	// соединить uvm_analyse_port command_monitor-а с analyse_port-ом 
	// uvm_tlm_analysis_fifo объекта cmd_f через метод try_get()
	command_monitor_h.ap.connect(scoreboard_h.cmd_f.analysis_export);
endfunction : connect_phase
```

![[Pasted image 20250511233317.png]]

