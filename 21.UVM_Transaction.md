# 21. UVM_Transaction

Поскольку компоненты тестбенча используют одни и те же данные (транзакции передаваемые в dirver, scoreboard, coverage и т.д.) то было бы логично передавать не структуры, а объекты класса uvm_transaction. 

`uvm_transaction` наследник `uvm_object`, из-за чего конструктор класса выглядит проще: 
```systemverilog
function new (string name ="");
    super.new(name);
endfunction
```

Если между компонентами передаются объекты, то было бы хорошо иметь методы, для создания копии объекта и по копированию полей в пустой объект. UVM предоставляет эти методы: 
`copy()` - копирование полей объекта в другой объект такого же класса
`clone()` - создает новый объект, заполняет его полями, и возвращают ссылку на созданный объект 

В чем разница между методами? 
`copy()` - когда нужно переопределить (перезаписать) поля существующего объекта (target) полями другого объекта (source)

`clone()` - когда создаем копию (source) объекта и возвращаем сслыку на нее, которую после через downcasting ($cast()) присваиваем в нужный нам placeholder 

## 1. Метод copy()
Метод `copy()` вызывает метод `do_copy()`, который должен быть переопределен в наследнике `uvm_transaction`. Ниже представлена реализация из книги: 
```verilog
function void do_copy(uvm_object rhs);
// uvm_object rhs - это требования UVM, rhs - right hand side
    command_transaction copied_transaction_h;

// проверка на существование объекта 
    if(rhs == null) 
    `uvm_fatal("COMMAND TRANSACTION", "Tried to copy from a null pointer")
// проверка совместимости типов 
    if(!$cast(copied_transaction_h,rhs))
    `uvm_fatal("COMMAND TRANSACTION", "Tried to copy wrong type.")

// инициализация полей родителей полями из передаваемого объекта 
    super.do_copy(rhs); // copy all parent class data

// копирование данных в поля текущего класса 
    A = copied_transaction_h.A;
    B = copied_transaction_h.B;
    op = copied_transaction_h.op;
endfunction : do_copy
```

## 2. Метод clone() 

Аналогично методу copy(), должна быть переопределена пользователем. Реализация может быть такая: 

```verilog

function command_transaction clone_me();
    command_transaction clone;
    uvm_object tmp;

    tmp = this.clone();
    $cast(clone, tmp);
    return clone;
endfunction : clone_me
```

Данный метод работает следующим образом: 
Объявляется тип возвращаемого значения функции - placeholder текущего класса (`command_transaction`), объявляется переменная этого типа `command_transaction clone`, объявляется переменная placeholder-а базового класса (`uvm_object tmp`), этой базовой переменной присваивается ссылка на скопированной объект, затем происходит каст базовой ссылку к типу ссылки класса, после чего возвращается значение переменной placeholder-а класса (`return clone`). 

## 3. Метод do_compare()

Данный метод переопределяется пользователем и позволяет сравнивать объект между собой. 

```verilog
function bit do_compare(uvm_object rhs, uvm_comparer comparer);
    command_transaction compared_transaction_h; // placeholder текущего класса
    bit   same;
    
    if (rhs==null) `uvm_fatal("RANDOM TRANSACTION", 
                            "Tried to do comparison to a null pointer");
    
    if (!$cast(compared_transaction_h,rhs))
    same = 0;
    else
    same = super.do_compare(rhs, comparer) && 
            (compared_transaction_h.A == A) &&
            (compared_transaction_h.B == B) &&
            (compared_transaction_h.op == op);
            
    return same;
endfunction : do_compare
```

rhs - ссылка на передаваемый объект, comparer - ссылка на текущий объект. Возвращает 1'b1 в случае совпадения, 1'b0 в случае не совпадения. Вызов данной функции следующий: 

```verilog
do_compare(rhs, comparer);
```

## 4. Мысли об tlm

До введения объектов в тестбенч, данные между компонентами передавались как стуктуры. Но проблема структур в том, что они статические, а объекты динамические, могут меняться в тетсбенче, шариться по ссылке и наследовать методы и поля. Таким образом, например, можно совсем абстрагироваться от протокола на котором работает дизайн, так как через драйвер передается объект (имеется ввиду его поля), параллельно с этим этот же объект передается в монитор, где данные извлекаются из DUT-a и передаются в компоненты scoreboard, coverage и т.д. Таким образом scoreboard может выглядит так: принимает от sequence транзакцию, накладывает правила дизайна по обработке полей, и сравнивает этот результат с данными, получаемыми из монитора. А самое потрясающие - объект аллоцирован один раз, меняется только sequencer-ом, а все остальные компоненты через ссылку получают доступ к нему, без копирования всего объекта. Получается, что с DUT взаимодействует только driver и monitor, остальные компоненты через uvm_tlm_fifo получают доступ к этим классам. 

Класс транзакции определяется вендором и поставляется внутри VIP-a. 

## 5. Пример env с классами

```verilog
class env extends uvm_env;
   `uvm_component_utils(env);

   tester    tester_h;
   coverage  coverage_h;
   scoreboard scoreboard_h;
   driver    driver_h;
   command_monitor command_monitor_h;
   result_monitor result_monitor_h;
   uvm_tlm_fifo #(command_transaction) command_f;
   
   function new (string name, uvm_component parent);
      super.new(name,parent);
   endfunction : new

   function void build_phase(uvm_phase phase);
   // аллоцировать tlm_fifo с классом uvm объекта 
      command_f = new("command_f", this);

  // аллоцировать объекты классов
      tester_h    = tester::type_id::create("tester_h",this);
      driver_h    = driver::type_id::create("driver_h",this);
      coverage_h  =  coverage::type_id::create ("coverage_h",this);
      scoreboard_h = scoreboard::type_id::create("scoreboard_h",this);
      command_monitor_h = command_monitor::type_id::create("command_monitor_h",this);
      result_monitor_h = result_monitor::type_id::create("result_monitor_h",this);      
   endfunction : build_phase

   function void connect_phase(uvm_phase phase);
   // подсоединить драйвер с тестером через tlm_fifo
      driver_h.command_port.connect(command_f.get_export);
      tester_h.command_port.connect(command_f.put_export);

   // подсоединить write метод мониторов c analysis портами coverage и scoreboard
      command_f.put_ap.connect(coverage_h.analysis_export);
      command_monitor_h.ap.connect(scoreboard_h.cmd_f.analysis_export);
      result_monitor_h.ap.connect(scoreboard_h.analysis_export);
   endfunction : connect_phase

   function void end_of_elaboration_phase(uvm_phase phase);
   // прописать verbosity uvm_info в scoreboard
      scoreboard_h.set_report_verbosity_level_hier(UVM_HIGH);
   endfunction : end_of_elaboration_phase

endclass
```