# 23.UVM_Sequences
В главе был введен новый класс `uvm_transaction`, необходимый для описания простых транзакций, то есть немного полей и методов. Его наследник, `uvm_sequence_item`, расширяет этот класс добавляя методы для межкомпонентного взаимодействия. 
На основе объекта этого класса работают классы `uvm_sequence`, `uvm_sequencer`, и `uvm_driver`, так как он является базой для передачи данных от "генератора" до "драйвера". 

Данный класс описывает поля транзакции. То есть если мы говорим об AXI транзакции, то на уровне этого классы мы абстрагируемся от физического уровня передачи к передачи только данных, то есть AWADDR, AWID, WDATA, ARADDR, RDATA и так далее. 

`uvm_sequence` отвечает за заполнение этого объекта данными и за управление потоком выполнения. 
Разница между классами:
```systemverilog
uvm_sequence_item // наследник uvm_transaction, объект транзакции
uvm_sequence      // наполнение транзакции данными, управление 
                  // последовательностью теста
uvm_sequencer     // компонент агента, управляющий очередью секвенций на драйвер 
```

![uvm_sequencer](./uvm_sequencer.svg)

## 1.Пример класса `uvm_sequence_item`:
```systemverilog
class sequence_item extends uvm_sequence_item;
	`uvm_object_utils(sequence_item)
	
	function new(string name = "");
		super.new(name);
	endfunction : new

	rand byte unsigned A;
	rand byte unsigned B;
	rand operation_t op;
	shortint unsigned result;

	constraint op_con {
		op dist {no_op := 1, add_op := 5, and_op:=5,
		xor_op:=5,mul_op:=5, rst_op:=1};
	}
  
	constraint data { 
		A dist {8'h00:=1, [8'h01 : 8'hFE]:=1, 8'hFF:=1};
		B dist {8'h00:=1, [8'h01 : 8'hFE]:=1, 8'hFF:=1};
	}
	extend function bit do_compare(uvm_object rhs, uvm_comparer comparer);
	
	extend function void do_copy(uvm_object rhs);

	extend function string convert2string();

endclass
```
Помимо добавления полей и констрейнов, класс, описывающий объект транзакции, добавляет методы для обработки полей объекта 
`do_compare(), do_copy(), convert2string()`.

## 2.Пример класса `uvm_sequencer`:
```systemverilog
// Объявления alias для базового класса uvm_sequencer.
// То есть теперь везде будет использоваться переменная sequencer
typedef uvm_sequencer #(sequence_item) sequencer;
```
В примерах используется класс uvm_sequencer без использования наследников 
## 3. Пример класса `uvm_driver`:

```systemverilog
class driver extends uvm_driver #(sequence_item)
	`uvm_component_utils(driver)

	virtual tinyalu_bfm bfm;

	function void build_phase(uvm_phase phase);
		// получение ссылки на интерфейс
		if(!uvm_config_db #(virtual tinyalu_bfm)::get(null, "*","bfm", bfm))
			`uvm_fatal("DRIVER", "Failed to get BFM")
	endfunction : build_phase

	task run_phase(uvm_phase phase);
		sequence_item cmd;
		forever begin : cmd_loop
			shortint unsigned result;
			// получить транзакцию из tlm-порта и положить в локальный объект
			seq_item_port.get_next_item(cmd);
			// отправить транзакцию в bfm и забрать из него result
			bfm.send_op(cmd.A, cmd.B, cmd.op, result);
			// положить result в локальный объект 
			cmd.result = result;
			// сообщить sequencer что обработка транзакции завершена 
			seq_item_port.item_done();
		end : cmd_loop
	endtask : run_phase

	function new (string name, uvm_component parent);
		super.new(name, parent);
	endfunction : new

endclass : driver
```

Первым делом класс `driver` наследуется с фиксированным типом объекта транзакции `sequence_item`.  Данный класс предоставляет переменную tlm-порта `seq_item_port`, позволяющий делать обработку принимаемого объекта, в данном случае объекта класса `sequence_item`. 
В большинстве случаев, VIP-ы представляют из себя уже готовые агенты с driver, monitor, sequencer и coverage (базовый сбор покрытия). Как некое API, вендоры предоставляют класс `agent_config` для настройки агента и класс `uvm_sequence_item`, который принимает sequencer и уже заполненный данными отправляет монитор VIP-a. 

## 4. Пример env с  подключением `sequencer` к `driver`

Полагаю, что данное подключение должно осуществляться в классе агента
```systemverilog
class env extends uvm_env;
	`uvm_component_utils(env)

	sequencer sequencer_h;
	driver driver_h;

	function new (string name, uvm_component parent);
		super.new(name,parent);
	endfunction : new

	function void build_phase(uvm_phase phase);
		sequencer_h = new("sequencer_h",this);
		driver_h = driver::type_id::create("driver_h",this);
	endfunction : build_phase

	function void connect_phase(uvm_phase phase);
		driver_h.seq_item_port.connect(sequencer_h.seq_item_export);
	endfunction : connect_phase

endclass : env
```
Подсоединяем tlm-порт драйвера `seq_item_port` с tlm-портом sequencer `seq_item_export`. В данных классах уже реализованы tlm-порты, из-за чего не нужно объявлять tlm-порт для подключения этих объектов 

## 5. Пример класса `uvm_sequence`

Данный класс находится вне иерархии UVM и в конструкторе класса не нужно указывать родителя. От базового класса наследует 3 поля: 
`m_sequencer` - placeholder для объекта класса sequencer 
`task body()` - таск для описания секвенции
`start_item()/finish_item()` - эти методы управляют sequencer и отправляют в него секвенции

В наследнике этого класса нужно переопределить конструктор и  body()
```systemverilog
class fibonacci_sequence extends uvm_sequence #(sequence_item);
	`uvm_object_utils(fibonacci_sequence)

	function new(string name = "fibonacci");
		super.new(name);
	endfunction : new

	task body();
		byte unsigned n_minus_2=0;
		byte unsigned n_minus_1=1;

		// объявляем ссылку на объект, который будет заполняться полями 
		sequence_item command;
		// создаем объект 
		command = sequence_item::type_id::create("command");

		start_item(command);
		command.op = rst_op;
		finish_item(command);

		`uvm_info("FIBONACCI", " Fib(01) = 00", UVM_MEDIUM);
		`uvm_info("FIBONACCI", " Fib(02) = 01", UVM_MEDIUM);
		
		for(int ff = 3; ff<=14; ff++) begin
			start_item(command);
			command.A = n_minus_2;
			command.B = n_minus_1;
			command.op = add_op;
			finish_item(command);
			n_minus_2 = n_minus_1;
			n_minus_1 = command.result;
			`uvm_info("FIBONACCI", $sformatf("Fib(%02d) = %02d", ff, n_minus_1),
																	UVM_MEDIUM);
		end
	endtask : body

endclass : fibonacci_sequence
```

`start_item()` - запрос доступа в sequencer с таким-то объектом. До получения разрешения поток исполнения блокируется 
После получения доступа, sequence настраивает поля объекта 
```systemverilog
start_item(command);
command.A = n_minus_2; // значение в поле А
command.B = n_minus_1; // значение в поле B
command.op = add_op;   // значение в поле op
finish_item(command);
```
`finish_item()` - блокирование потока исполнения до момента, пока sequencer не отправит транзакцию в драйвер и не отправить подтверждение через `seq_item_port.item_done()` в классе драйвера. 
Поскольку ссылка на объект транзакции сохраняется, то из него можно вычитать данные после завершения `finish_item()`.

## 6.Управление sequence из uvm_test

Для управления sequence из `uvm_test` в классе теста нужно объявить ссылки на эти объекты, создать объекты, а после приравнять локальные ссылки на объект  секвенции (placeholder-ы) с ссылками на объекты в  классе env. 
```systemverilog
class fibonacci_test extends uvm_test;
	`uvm_component_utils(fibonacci_test)
	env env_h;
	sequencer sequencer_h;
	
	function void build_phase(uvm_phase phase);
		env_h = env::type_id::create("env_h",this);
	endfunction : build_phase
	  
	function void end_of_elaboration_phase(uvm_phase phase);
	// присвоить локальной ссылке ссылку на объект из класса env
		sequencer_h = env_h.sequencer_h;
	endfunction : end_of_elaboration_phase
	
	function new (string name, uvm_component parent);
		super.new(name,parent);
	endfunction : new
	
	task run_phase(uvm_phase phase);
		fibonacci_sequence fibonacci;
		fibonacci = new("fibonacci");
		
		phase.raise_objection(this);
		fibonacci.start(sequencer_h);
		phase.drop_objection(this);
	endtask : run_phase
endclass
```
В `run_phase` объявляется placeholder на объект секвенции, создается объект, делается подъём возражений и в объект секвенции передается ссылка на объект `sequencer` через метод `.start()`. Таким образом, мы говорим секвенции `fibonacci` когда запуститься, и в какой объект `uvm_sequencer` направить сформированную транзакцию. То есть подключение `sequence` к `sequencer` происходит через `uvm_test`.

Но что если инкапсулировать подключение `sequences` к `sequencer` агента в  отдельном классе? Таким образом, нам не придется иерархически соединять последовательность с `sequence` в `uvm_test`. `uvm_test` становится еще большим начальником, таким, ящиком готовых инструментом. Ему не нужно знать как работают инструменты, ему нужен только список инструментов, которые сейчас положить в набор и использовать. Этим списком является переопределение наследников при той же структуре тестбенча. Отсюда мы подходим к такому понятию, как виртуальная секвенция. 

## 7.Виртуальная секвенция 

Название такое пошло от того, что данная секвенция собирает все существующие в одном месте, соединяет их с `sequencer` агентов, и задает программу исполнения. Данный класс инкапсулирует в себе все подключение, из-за чего в `uvm_test` все последовательности запускаются одним вызовом: 

```systemverilog
runall_sequence runall_seq;

task run_phase(uvm_phase phase);
	runall_seq = new("runall_seq");
	phase.raise_objection(this);
	// где sequencer уже подключен в секвенции, из-за чего ссылка больше не нужна
	runall_seq.start(null);
	phase.drop_objection(this);
endtask : run_phase
```

Пример виртуальной секвенции: 
```systemverilog
class runall_sequence extends uvm_sequence #(uvm_sequence_item);
	`uvm_object_utils(runall_sequence)

// создание ссылок на объекты все секвенций
// данные ссылки нельзя переопределить в наследнике 
	protected reset_sequence reset;
	protected maxmult_sequence maxmult;
	protected random_sequence random;
	protected sequencer sequencer_h;
	protected uvm_component uvm_component_h;

// Конструктор класса упрощенный, всего один аргумент, текущее имя 
	function new(string name = "runall_sequence");
		super.new(name);
		
		uvm_component_h = uvm_top.find("*.env_h.sequencer_h");
	
		if (uvm_component_h == null)
			`uvm_fatal("RUNALL SEQUENCE", "Failed to get the sequencer")  
		if (!$cast(sequencer_h, uvm_component_h))
			`uvm_fatal("RUNALL SEQUENCE", "Failed to cast from uvm_component_h.")
		
		reset = reset_sequence::type_id::create("reset");
		maxmult = maxmult_sequence::type_id::create("maxmult");
		random = random_sequence::type_id::create("random");
	endfunction : new

	task body();
		reset.start(sequencer_h);
		maxmult.start(sequencer_h);
		random.start(sequencer_h);
	endtask : body

endclass : runall_sequence
```

В данной секвенции объявляются ссылки на все объекты, причем так, что их нельзя отменить в наследнике. Через метод `uvm_top.find("*.path")` находиться ссылка на объект `sequencer` агента випа, данная ссылка присваивается в placeholder базового класса, откуда потом через cast данная ссылка приводится к типу интересующего нас секвенсера. Затем создаются объекты секвенций. Полученная ссылка на `sequencer` без дела не пропадает, она используется для передачи в объекты секвенций. Таким образом каждая секвенция получает доступ к `sequncer-у` агента.  При этом метод body() ни чем не ограничен, и  в нем можно построить любую последовательность доступа секвенций к `sequencer`. 

НО этот метод иногда не так хорош, потому что пути до `sequencer` могут поменяться. Поэтому внутри секвенции можно использовать переменную `m_sequencer`, хранящую ссылку на реальную секвенцию: 

```systemverilog
class parallel_sequence extends uvm_sequence #(uvm_sequence_item);
	`uvm_object_utils(parallel_sequence)

	protected reset_sequence reset;
	protected short_random_sequence short_random;
	protected fibonacci_sequence fibonacci;
  
	function new(string name = "parallel_sequence");
		super.new(name);
		
		reset = reset_sequence::type_id::create("reset");
		fibonacci = fibonacci_sequence::type_id::create("fibonacci");
		short_random = short_random_sequence::type_id::create("short_random");
	endfunction : new

	task body();
	// ссылка на реальный sequencer кладет в поле класса m_sequencer
		reset.start(m_sequencer);
		fork
			fibonacci.start(m_sequencer);
			short_random.start(m_sequencer);
		join
	endtask : body

endclass : parallel_sequence
```

Запуск виртуальной секвенции из `uvm_test` с передачей ссылки `sequencer`

```systemverilog
task run_phase(uvm_phase phase);
	phase.raise_objection(this);
// через метод start() ссылка на секвенсер присваивается в m_sequencer
	parallel_h.start(sequencer_h);
	phase.drop_objection(this);
endtask : run_phase
```
